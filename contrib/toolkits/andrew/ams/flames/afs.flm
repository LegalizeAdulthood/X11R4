;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;                                                                   ;;;;
;;;;       Copyright IBM Corporation 1988,1989 - All Rights Reserved   ;;;;
;;;;      For full copyright information see:'andrew/config/COPYRITE'  ;;;;
;;;;                                                                   ;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;
;;;;               ;;;;
;;;; AFS services  ;;;;
;;;;               ;;;;
;;;;;;;;;;;;;;;;;;;;;;;

;;
;; need to get "BIT-AND" and "ZEROP" and "DOLIST" from elilib.
;;

(load "elilib")

;;
;; AFS rights bit twiddling routines.
;;   These are meant to be used with the UserRightsToDir and
;;   UserAnyRightToDir FLAMES primitives to translate to and
;;   from the integer rights code (0 - 127) and either a string
;;   representation (as used by the 'fs' command, e.g. "rlidwka")
;;   or a set-of-symbols representation (more lisp-like, e.g.
;;   '(read lookup insert delete write lock administer)).
;;

;;
;; This is the mapping from integer code to symbolic and string
;; forms.  The order matters in that translating from an integer
;; will result in strings and sets ordered by the list below.
;;
(setq *afs-rights-mapping*
      '((read "r" 1)
	(lookup "l" 8)
	(insert "i" 4)
	(delete "d" 16)
	(write "w" 2)
	(lock "k" 32)
	(administer "a" 64)))
;;
;; Access routines for above "table"
;;
(defun afs-get-symbol (x) (car x))
(defun afs-get-character (x) (cadr x))
(defun afs-get-int (x) (caddr x))

;;
;; STRING-TO-AFS-RIGHTS
;;   string s -- the string to translate to the integer rights
;; returns the integer rights translation of the string.
;;
(defun string-to-afs-rights (s)
  (let ((result 0))
    (dolist (afs-map *afs-rights-mapping* result)
	    (cond ((strcontains (afs-get-character afs-map) s t)
		   (setq result (+ result (afs-get-int afs-map))))))))
  )

;;
;; AFS-RIGHTS-TO-STRING
;;   integer r -- the rights to translate to a string
;; returns the string representation of the rights r.
;;
(defun afs-rights-to-string (r)
  (apply 'strcat
	 (mapcar '(lambda (afs-map)
		    (cond ((not (zerop (bit-and (afs-get-int afs-map) r)))
			   (afs-get-character afs-map))
			  (t "")))
		 *afs-rights-mapping*)))

;;
;; SET-TO-AFS-RIGHTS
;;   set-of-symbols s -- the symbolic rights tokens to translate
;; returns the integer rights translation of the set-of-symbols.
;;
(defun set-to-afs-rights (s)
  (let ((result 0))
    (dolist (afs-map *afs-rights-mapping* result)
	    (cond ((member (afs-get-symbol afs-map) s)
		   (setq result (+ result (afs-get-int afs-map))))))))

;;
;; AFS-RIGHTS-TO-SET
;;   integer r -- the rights to translate to a set-of-symbols
;; returns the set-of-symbols representation of the rights r.
;;
(defun afs-rights-to-set (r)
  (apply 'append
	 (mapcar '(lambda (afs-map)
		    (cond ((not (zerop (bit-and (afs-get-int afs-map) r)))
			   (list (afs-get-symbol afs-map)))))
		 *afs-rights-mapping*)))

